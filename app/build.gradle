plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def hasReleaseKeystore =
        System.getenv("RELEASE_KEYSTORE_PATH") &&
        System.getenv("RELEASE_KEYSTORE_PASSWORD") &&
        System.getenv("RELEASE_KEY_ALIAS") &&
        System.getenv("RELEASE_KEY_PASSWORD")

android {
    namespace 'core.coreshift.policy'
    compileSdk 34

    defaultConfig {
        applicationId "core.coreshift.policy"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        if (hasReleaseKeystore) {
            release {
                storeFile file(System.getenv("RELEASE_KEYSTORE_PATH"))
                storePassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            debuggable false
            minifyEnabled false
            shrinkResources false

            if (hasReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += ["lib/**/libcoreshift.so"]
        }
    }
}

dependencies {}
